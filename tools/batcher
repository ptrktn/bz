#!/usr/bin/expect
# -*-tcl-*-

proc err {msg} {
	puts stderr "Error: $msg"
	exit 1
}

proc dbg {msg} {
	global cfg

	puts stdout $msg
}

proc batcher_init {} {
	global cfg

	set cfg(inifile) {~/.batcherrc}
	set cfg(prompt) "\[#>\$\] "

	if {[file exists $cfg(inifile)]} {
		global cfg
		source $cfg(inifile)
	} else {
		err "init file not found"
	}

	dbg [array names cfg]

	# FIXME check gate-srv completeness
}

proc batcher_shell {args} {
	global cfg

	if {[llength $args] < 1} {
		err "shell: missing argument list"
	}

	foreach i $args {
		dbg "shell: $i"
	}

	set srv $cfg(srv)

	if {[info exists cfg($srv,gate)]} {
		set serv1 $cfg($cfg($srv,gate),srv)
		set pass1 $cfg($cfg($srv,gate),passwd)
		set serv2 $cfg($srv,srv)
		set pass2 $cfg($srv,passwd)
	} else {
		set serv1 $cfg($srv,srv)
		set pass1 $cfg($srv,passwd)
	}

	spawn ssh -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $serv1
	expect {password: }
	send "$pass1\r"
	expect $cfg(prompt)

	if {[info exists cfg($srv,gate)]} {
		send "ssh -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $serv2\r"
		expect {password: }
		send "$pass2\r"
		expect $cfg(prompt)
	}

	foreach i $args {
		send "$i\r"
		expect $cfg(prompt)
	}

	if {[info exists cfg($srv,gate)]} {
		send "exit\r"
	}

	send "exit\r"

	dbg "\nshell: completed"
}


proc batcher_get {args} {
	global cfg
	
	if {[llength $args] < 1} {
		err "get: missing argument list"
	}

	set srv $cfg(srv)

	# copy via gate server if used
	if {[info exists cfg($srv,gate)]} {
		set serv1 $cfg($cfg($srv,gate),srv)
		set pass1 $cfg($cfg($srv,gate),passwd)
		set serv2 $cfg($srv,srv)
		set pass2 $cfg($srv,passwd)

		spawn ssh -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $serv1
		expect {password: }
		send "$pass1\r"
		expect {$ }

		# FIXME use sftp
		foreach i $args {
			set fname [file tail $i]
		
			send "scp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $serv2:$i .\r"
			expect {password: }
			send "$pass2\r"
			expect {$ }
			
			dbg "get: $i transfer to gate server completed"
		}

		send "exit\r"
		expect eof

		dbg "get: transfer of all files to gate server completed"
	}

	# copy from server or from gate server

	if {[info exists cfg($srv,gate)]} {
		set serv $cfg($cfg($srv,gate),srv)
		set pass $cfg($cfg($srv,gate),passwd)
	} else {
		set serv $cfg($srv,srv)
		set pass $cfg($srv,passwd)
	}
		
	spawn sftp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $serv
	expect {password: }
	send "$pass\r"
	expect {sftp> }

	foreach i $args {
		if {[info exists cfg($srv,gate)]} {
			set fname [file tail $i]
		} else {
			set fname $i
		}

		send "get $fname\r"
		expect {sftp> }

		if {[info exists cfg($srv,gate)]} {
			send "rm $fname\r"
			expect {sftp> }
		}
	}

	send "quit\r"
	expect eof

	dbg "\nget: transfer completed"
}

# FIXME use sftp
proc batcher_put {args} {
	global cfg
	
	if {[llength $args] < 1} {
		err "put: missing argument list"
	}

	set srv $cfg(srv)

	foreach i $args {
		if {![file exists $i]} {
			err "put: file $i not found"
		}
		lappend fnames [file tail $i]
	}

	if {[info exists cfg($srv,gate)]} {
		set serv $cfg($cfg($srv,gate),srv)
		set pass $cfg($cfg($srv,gate),passwd)
	} else {
		set serv $cfg($srv,srv)
		set pass $cfg($srv,passwd)
	}

	# copy to remote server or to gate server - mind the {*}
	# https://stackoverflow.com/questions/21407177/tcl-expect-multiple-files-w-scp
	spawn scp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no {*}$args $serv:
	expect {password: }
	send "$pass\r"

	# copy to final destination if gate server is used
	if {[info exists cfg($srv,gate)]} {

		dbg "\nput: [join $fnames] transfer to gate server completed"

		set serv1 $cfg($cfg($srv,gate),srv)
		set pass1 $cfg($cfg($srv,gate),passwd)
		set serv2 $cfg($srv,srv)
		set pass2 $cfg($srv,passwd)

		spawn ssh -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $serv1
		expect {password: }
		send "$pass1\r"
		expect {$ }
		send "scp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no [join $fnames] $serv2:\r"
		expect {password: }
		send "$pass2\r"
		expect {$ }
		send "rm -f [join $fnames]\r"
		expect {$ }
		send "exit\r"
	}

	dbg "\nput: transfer completed"
}


proc batcher_main {} {
    global argv
    global env
	global cfg

	batcher_init

    foreach i $argv {
		dbg "ARGV $i"
    }

	set srv [lindex $argv 0]
	set cmd [lindex $argv 1]

	dbg "srvt $srv cmd $cmd"

	if {![info exists cfg($srv,srv)]} {
		err "server $srv undefined"
	}

	if {[string compare [info procs "batcher_$cmd"] "batcher_$cmd"]} {
		err "command $cmd undefined"
	}

	set cfg(srv) $srv

	eval batcher_$cmd [lrange $argv 2 end]
}

batcher_main

exit 0
