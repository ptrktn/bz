#!/usr/bin/expect
# -*-tcl-*-
#
# NAME
#  batcher
# 
# SYNOPSIS
#  batcher HOST get FILE...
#  batcher HOST put FILE...
#  batcher HOST cmd COMMAND...
#
# DESCRIPTION
#  This tool streamlines access to hosts in which keyboard-interactive
#  authentication is forced.
#
# FILES
#  ~/.batcherrc
#  This is a Tcl file for defining hosts and gateways.
#
#  A host named 'poseidon' to which user can access directly.  
#
#   set cfg(host,poseidon) poseidon.local
#   set cfg(pass,poseidon) Secret123
#
# A host named 'zeus' to which one can access through host
# named `hera'.
#
#   set cfg(host,zeus) zeus.local
#   set cfg(pass,zeus) Secret123
#   set cfg(gate,zeus) hera
#   set cfg(host,hera) hera.local
#   set cfg(pass,hera) 456Secret
#
# EXAMPLES
#   Copy files to remote host and unpack them.
#     batcher zeus put file1.zip file2.zip 
#     batcher zeus cmd "unzip -q file1.zip" "unzip -q file2.zip"
#
# COMPATIBILITY
#   expect 5.45

proc err {msg} {
	puts stderr "Error: $msg"
	exit 1
}

proc dbg {msg} {
	global cfg

	puts stdout $msg
}

proc batcher_init {} {
	global cfg

	set cfg(inifile) {~/.batcherrc}
	set cfg(prompt) "\[#>\$\] "

	if {[file exists $cfg(inifile)]} {
		global cfg
		source $cfg(inifile)
	} else {
		err "init file not found"
	}

	dbg [array names cfg]

	# FIXME check host-gate completeness
}

proc batcher_cmd {args} {
	global cfg

	if {[llength $args] < 1} {
		err "cmd: missing argument list"
	}

	foreach i $args {
		dbg "cmd: $i"
	}

	set host $cfg(host)

	if {[info exists cfg(gate,$host)]} {
		set host1 $cfg(host,$cfg(gate,$host))
		set pass1 $cfg(pass,$cfg(gate,$host))
		set host2 $cfg(host,$host)
		set pass2 $cfg(pass,$host)
	} else {
		set host1 $cfg(host,$host)
		set pass1 $cfg(pass,$host)
	}

	spawn ssh -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $host1
	expect {password: }
	send "$pass1\r"
	expect $cfg(prompt)

	if {[info exists cfg(gate,$host)]} {
		send "ssh -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $host2\r"
		expect {password: }
		send "$pass2\r"
		expect $cfg(prompt)
	}

	foreach i $args {
		send "$i\r"
		expect $cfg(prompt)
	}

	if {[info exists cfg(gate,$host)]} {
		send "exit\r"
	}

	send "exit\r"

	dbg "\ncmd: completed"
}


proc batcher_get {args} {
	global cfg
	
	if {[llength $args] < 1} {
		err "get: missing argument list"
	}

	set host $cfg(host)

	# copy via gate host if used
	if {[info exists cfg(gate,$host)]} {
		set host1 $cfg(host,$cfg(gate,$host))
		set pass1 $cfg(pass,$cfg(gate,$host))
		set host2 $cfg(host,$host)
		set pass2 $cfg(pass,$host)

		spawn ssh -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $host1
		expect {password: }
		send "$pass1\r"
		expect $cfg(prompt)

		# FIXME use sftp
		foreach i $args {
			set fname [file tail $i]
		
			send "scp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $host2:$i .\r"
			expect {password: }
			send "$pass2\r"
			expect $cfg(prompt)
			
			dbg "get: $i transfer to gate host completed"
		}

		send "exit\r"
		expect eof

		dbg "get: transfer of all files to gate host completed"
	}

	# copy from server or from gate server

	if {[info exists cfg(gate,$host)]} {
		set host1 $cfg(host,$cfg(gate,$host))
		set pass1 $cfg(pass,$cfg(gate,$host))
	} else {
		set host1 $cfg(host,$host)
		set pass1 $cfg(pass,$host)
	}
		
	spawn sftp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $host1
	expect {password: }
	send "$pass1\r"
	expect {sftp> }

	foreach i $args {
		if {[info exists cfg(gate,$host)]} {
			set fname [file tail $i]
		} else {
			set fname $i
		}

		send "get $fname\r"
		expect {sftp> }

		if {[info exists cfg(gate,$host)]} {
			send "rm $fname\r"
			expect {sftp> }
		}
	}

	send "quit\r"
	expect eof

	dbg "\nget: transfer completed"
}

# FIXME use sftp
proc batcher_put {args} {
	global cfg
	
	if {[llength $args] < 1} {
		err "put: missing argument list"
	}

	set host $cfg(host)

	foreach i $args {
		if {![file exists $i]} {
			err "put: file $i not found"
		}
		lappend fnames [file tail $i]
	}

	if {[info exists cfg(gate,$host)]} {
		set host1 $cfg(host,$cfg(gate,$host))
		set pass1 $cfg(pass,$cfg(gate,$host))
	} else {
		set host1 $cfg(host,$host)
		set pass1 $cfg(pass,$host)
	}

	# copy to remote server or to gate server - mind the {*}
	# https://stackoverflow.com/questions/21407177/tcl-expect-multiple-files-w-scp
	spawn scp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no {*}$args $host1:
	expect {password: }
	send "$pass1\r"

	# copy to final destination if gate server is used
	if {[info exists cfg(gate,$host)]} {

		dbg "\nput: [join $fnames] transfer to gate server completed"

		set host1 $cfg(host,$cfg(gate,$host))
		set pass1 $cfg(pass,$cfg(gate,$host))
		set host2 $cfg(host,$host)
		set pass2 $cfg(pass,$host)

		spawn ssh -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no $host1
		expect {password: }
		send "$pass1\r"
		expect $cfg(prompt)

		send "scp -q -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no -o CheckHostIP=no [join $fnames] $host2:\r"
		expect {password: }
		send "$pass2\r"
		expect $cfg(prompt)

		send "rm -f [join $fnames]\r"
		expect $cfg(prompt)
		send "exit\r"
	}

	dbg "\nput: transfer completed"
}


proc batcher_main {} {
    global argv
    global env
	global cfg

	batcher_init

    foreach i $argv {
		dbg "ARGV $i"
    }

	set host [lindex $argv 0]
	set cmd [lindex $argv 1]

	dbg "host $host cmd $cmd"

	if {![info exists cfg(host,$host)]} {
		err "host $host undefined"
	}

	if {[string compare [info procs "batcher_$cmd"] "batcher_$cmd"]} {
		err "cmd $cmd undefined"
	}

	set cfg(host) $host

	eval batcher_$cmd [lrange $argv 2 end]
}

batcher_main

exit 0
